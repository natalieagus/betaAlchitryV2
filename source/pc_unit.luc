module pc_unit (
    input clk,  // clock
    input rst,  // reset
    input slowclk, // slowclock to advance pc
    input id[16],
    input pcsel[3],
    input reg_data_1[32],
    output pc_4[32],
    output pc_4_sxtc[32],
    output pcsel_out[32], // for debug only, does not exist in pc_unit
    output ia[32]
) {
    
    dff pc[32](#INIT(0),.clk(clk)) // PC Register
    
    sig pcsel_out_sig[32]
    sig pc_4sxtc3_2sig[32]
    sig pc_4sxtc_sig[32]
    sig pc_4sig[32]
    sig pc_4raw[32]
    
    always {
        
        // increment pc by 4
        pc_4raw = pc.q + 4
        pc_4sig = c{pc.q[31], pc_4raw[30:0]}            
        
        // TASK 4
        // shift-and-add pc by 4
        pc_4sxtc3_2sig = pc.q + 4 + (4 * c{16x{id[15]}, id[15:0]})
        pc_4sxtc_sig = c{pc.q[31], pc_4sxtc3_2sig[30:0]}
        
        // TASK 1 
        // PCSEL mux 
        case (pcsel){
            b000: 
                pcsel_out_sig = pc_4sig
            b001:
                pcsel_out_sig = pc_4sxtc_sig
            b010:
                // JMP mux to protect JT31
                pcsel_out_sig = c{pc.q[31] & reg_data_1[31], reg_data_1[30:0]}
            b011:
                pcsel_out_sig = h80000004 // illop 
            b100: 
                pcsel_out_sig = h80000008 // irq 
            default:
                pcsel_out_sig = pc.q
        }
        
        // advance the PC only when slowclk is 1 
        if (slowclk){
            pc.d = c{pcsel_out_sig[31:2], b00} // setting of pcreg content must happen only when slowclk == 1, don't bring this outside of if (slowclk) clause
        }
        
        // Task 2
        // RESET mux 
        if (rst){
            pc.d = h80000000 // reset can happen anytime regardless of slowclk
        }
        
        // output connections
        ia = pc.q
        pc_4 =  pc_4sig
        pc_4_sxtc = pc_4sxtc_sig
        pcsel_out = pcsel_out_sig
        
        
    }
}